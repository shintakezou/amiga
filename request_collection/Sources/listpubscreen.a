;
; $VER: listpubscreen v1.0 (c)2003 by Mauro Panigada
;
; listpubscreen 1.0
; written by Mauro Panigada
;
; PURPOSE
;  list to the output names of public screens
;

                incdir  "inc:"
                include "exec/types.i"
                include "exec/libraries.i"
                include "exec/memory.i"
                include "exec/nodes.i"
                include "exec/lists.i"
                include "exec/exec_lib.i"
                include "dos/dos.i"
                include "dos/dosextens.i"
                include "dos/dos_lib.i"
                include "intuition/intuition.i"
                include "intuition/screens.i"
                include "intuition/intuition_lib.i"

                opt p=68020
                opt nodebug

call            MACRO
                jsr     (_LVO\1,a6)
                ENDM

execbase        MACRO
                move.l  (4).w,a6
                ENDM

getbase         MACRO
        IFEQ    '\1','dos'
                move.l  (a5),a6
        ELSE
                move.l  (\1base,a5),a6
        ENDC
                ENDM

        STRUCTURE mia,0
                LONG    dosbase
                LONG    intbase

                WORD    returncode_l
                BYTE    pad1
                BYTE    returncode

                LONG    first

                STRUCT  nome,256

                LABEL   mia_SIZEOF


        SECTION codice,CODE

                execbase
                move.l          #mia_SIZEOF,d0
                move.l          #MEMF_ANY|MEMF_CLEAR,d1
                call            AllocMem
                move.l          d0,a5
                tst.l           d0
                bne.s           start
                moveq           #RETURN_FAIL,d0
                rts

                dc.b    "$VER: listpubscreen 1.0 (c)2003 by Mauro Panigada",13
accapo          dc.b    10,0
                even

start
                lea             (dosname,pc),a1
                moveq           #39,d0
                call            OpenLibrary
                move.l          d0,(a5)
                beq             fine0

                lea             (intname,pc),a1
                moveq           #39,d0
                call            OpenLibrary
                move.l          d0,(intbase,a5)
                beq             fine0

                getbase         int
                lea             (nome,a5),a1
                suba.l          a0,a0
                call            NextPubScreen

                lea             (nome,a5),a0
                move.l          a0,a2
                call            LockPubScreen
                move.l          d0,(first,a5)

                move.l          d0,d6           ;prev
                beq             fine0

                move.l          a2,d1
                getbase         dos
                call            PutStr
                move.l          #accapo,d1
                call            PutStr

listloop
                move.l          d6,a0
                lea             (nome,a5),a1
                move.l          a1,a2
                getbase         int
                call            NextPubScreen

                move.l          a2,a0
                call            LockPubScreen
                move.l          d0,d5
                beq.s           fine

                cmp.l           (first,a5),d5
                beq.s           fineun

                move.l          d6,a0
                call            UnlockPubScreen

nounlock
                move.l          d5,d6

                move.l          a2,d1
                getbase         dos
                call            PutStr
                move.l          #accapo,d1
                call            PutStr

                execbase
                moveq           #0,d0
                move.l          #SIGBREAKF_CTRL_C,d1
                call            SetSignal
                and.l           #SIGBREAKF_CTRL_C,d0
                beq.s           listloop

fine1
                move.l          (first,a5),a1
                suba.l          a0,a0
                getbase         int
                call            UnlockPubScreen

fine

*************** FINE *********************************
fine0
                execbase
                move.l          (intbase,a5),d0
                beq.s           f_noint
                move.l          d0,a1
                call            CloseLibrary
f_noint
                move.l          (dosbase,a5),d0
                beq.s           f_nodos
                move.l          d0,a1
                call            CloseLibrary
f_nodos
                move.l          (returncode_l,a5),d2
                move.l          a5,a1
                move.l          #mia_SIZEOF,d0
                call            FreeMem
                move.l          d2,d0
                rts


fineun
                move.l          d5,a0
                call            UnlockPubScreen
                bra             fine1

*************** DATI **********************************
intname         dc.b    "intuition.library",0
dosname         dc.b    "dos.library",0


        END
